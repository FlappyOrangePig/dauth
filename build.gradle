// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    // Gradle will not find vars defined in an external file when referring to them
    // in the buildscript block, unless you link it from the buildscript block, too.
    apply from: 'config.gradle'
}

plugins {
    id 'com.android.application' version '8.0.2' apply false
    id 'com.android.library' version '8.0.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.20' apply false
    id "org.jetbrains.kotlin.kapt" version "1.8.20" apply false
    id "org.jetbrains.dokka" version "1.8.20" apply false
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion versions.compile_sdk
                buildToolsVersion versions.build_tools
                defaultConfig {
                    minSdk versions.min_sdk
                    targetSdk versions.target_sdk
                    versionCode versions.version_code
                    vectorDrawables.useSupportLibrary = true
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                    consumerProguardFiles "consumer-rules.pro"
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
                kotlinOptions { jvmTarget = '1.8' }
                buildFeatures { viewBinding = true }
                buildTypes {
                    release {
                        minifyEnabled true
                        zipAlignEnabled true
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                    debug {
                        minifyEnabled true
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }
            }
            println("namespace=" + android.namespace)
        }
    }
}

/**
 * 解决如下问题：
 * 'compileDebugJavaWithJavac' task (current target is 1.8) and 'kaptGenerateStubsDebugKotlin'
 * task (current target is 17) jvm target compatibility should be set to the same Java version.
 * 原文：
 * https://stackoverflow.com/questions/75650195/build-error-kspdebugkotlin-task-current-target-is-17
 */
allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

/*configurations.all {
    resolutionStrategy.force "com.cyberflow.dauthsdk:dauth-sdk-1.0.0-20230711.082503-1"
}*/
