apply plugin: 'maven-publish'

afterEvaluate { project ->
    tasks.all { task ->
        def taskName = task.name
        if (taskName.startsWith("publish") && taskName.endsWith("Repository")) {
            def dependent = "assembleRelease"
            task.dependsOn tasks.getByName(dependent)
            println(taskName + " depends on " + dependent)
        }

    }
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    //加载资源
    Properties properties = new Properties()
    InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
    properties.load(inputStream)
    publishing {
        publications {
            release(MavenPublication) {
                groupId = "com.cyberflow.dauthsdk"
                artifactId = project.getName()
                version = android.defaultConfig.versionName
                from components.release
                artifact sourceJar
            }
        }
        repositories {
            maven {
                name "yxRelease"
                url MAVEN_RELEASE_URL
                credentials {
                    username = properties.MAVEN_USER
                    password = properties.MAVEN_PASSWORD
                }
                allowInsecureProtocol = true
            }
            maven {
                name "yxSnapshot"
                url MAVEN_SNAPSHOT_URL
                credentials {
                    username = properties.MAVEN_USER
                    password = properties.MAVEN_PASSWORD
                }
                allowInsecureProtocol = true
            }
            maven {
                name "yxLocal"
                url rootProject.file("./repo")
            }
        }
    }
}
